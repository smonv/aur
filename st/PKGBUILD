# Maintainer: Tien Thanh <tthanh.v9.16@gmail.com>

pkgname=st-tthanh
_pkgname=st
pkgver=0.7.38.g0ac685f
pkgrel=1
epoch=
pkgdesc='Simple virtual terminal emulator for X'
arch=('x86_64')
url='http://git.suckless.org/st'
license=('MIT')
depends=('libxft' 'libxext')
makedepends=('ncurses'  'git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=('git://git.suckless.org/st'
		'https://st.suckless.org/patches/hidecursor/st-hidecursor-20170404-745c40f.diff'
		'config.h')
sha1sums=('SKIP' 'SKIP' 'SKIP')

pkgver(){
	cd "${srcdir}/${_pkgname}"
	git describe --tags |sed 's/-/./g'
}

prepare() {
	cd "${srcdir}/${_pkgname}"
	sed '/\@tic /d' -i Makefile
	cp "${srcdir}/config.h" 'config.h'
	git apply "${srcdir}/st-hidecursor-20170404-745c40f.diff"
}


build() {
	cd "${srcdir}/${_pkgname}"
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "${srcdir}/${_pkgname}"
	make PREFIX='/usr' DESTDIR="${pkgdir}" TERMINFO="${pkgdir}/usr/share/terminfo" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
